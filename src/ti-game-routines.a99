*********************************************************************
*
* Draw playfield
*
ti_draw_playfield:
       .proc
       bl   @ti_display_off
       seto @graphics_bank_select
       li   hl,playfield_data
ti_draw_playfield_1:
       mov  *hl+,tmp0
       jeq  ti_draw_playfield_2
       mov  *hl+,tmp1
       mov  *hl+,tmp2
       bl   @ti_draw_graphics
       jmp  ti_draw_playfield_1
ti_draw_playfield_2:
       li   hl,attributes
       li   de,zx_attributes
       li   bc,768
       clr  a
ti_draw_playfield_3:
       movb *hl+,a
       mov  de,tmp1
       bl   @ti_draw_attribute
       inc  de
       dec  bc
       jne  ti_draw_playfield_3
       bl   @ti_display_on
       .endproc
playfield_data:
       data (0*32+1)*8,gfx_slot_numbers_left,>0c01
       data (0*32+14)*8,gfx_centrepiece,>0403
       data (0*32+19)*8,gfx_slot_numbers_right,>0c01
       data (3*32+0)*8,gfx_doors_top,>2001
       data (4*32+0)*8,gfx_wall_left,>020b
       data (4*32+9)*8,gfx_wall_middle,>040b
       data (4*32+20)*8,gfx_wall_middle,>040b
       data (4*32+31)*8,gfx_door_frame_side,>010b
       data (15*32+0)*8,gfx_door_bottom,>2001
       data (17*32+0)*8,gfx_tellers,>2004
       data (22*32+1)*8,gfx_score_text_image,>0602
       data (21*32+16)*8,gfx_lives_text_image,>0603
       data 0
*// ti_draw_playfield

*********************************************************************
*
* Screen positions of the 3 doors
*
ti_door_positions:
       equ  $
ti_door_position_1:
       data  4*32+2
ti_door_position_2:
       data  4*32+13
ti_door_position_3:
       data 4*32+24

*********************************************************************
*
* Setup paramaters for calling ti_draw_door_frame_1-4
*
ti_setup_draw_door_params:
       clr  tmp1
       movb a,tmp1
       swpb tmp1
       clr  tmp2
       movb @door_frame_ref,tmp2
       swpb tmp2
       dec  tmp2
       rt
*// ti_setup_draw_door_params

*********************************************************************
*
* Draw door frame 1
*
* tmp1: character index to draw
* tmp2: door index 0-2
*
ti_draw_door_frame_1:
       .proc
       sla  tmp2,1
       mov  @ti_door_positions(tmp2),tmp0
       clr  tmp2
       bl   @ti_draw_character_and_door
       .endproc
*// ti_draw_door_frame_1

*********************************************************************
*
* Draw door frame 2
*
* tmp1: character index to draw
* tmp2: door index 0-2
*
ti_draw_door_frame_2:
       .proc
       sla  tmp2,1
       mov  @ti_door_positions(tmp2),tmp0
       li   tmp2,1
       bl   @ti_draw_character_and_door
       .endproc
*// ti_draw_door_frame_2

*********************************************************************
*
* Draw door frame 3
*
* tmp1: character index to draw
* tmp2: door index 0-2
*
ti_draw_door_frame_3:
       .proc
       sla  tmp2,1
       mov  @ti_door_positions(tmp2),tmp0
       li   tmp2,2
       bl   @ti_draw_character_and_door
       .endproc
*// ti_draw_door_frame_3

*********************************************************************
*
* Draw door frame 4
*
* tmp1: character index to draw
* tmp2: door index 0-2
*
ti_draw_door_frame_4:
       .proc
       sla  tmp2,1
       mov  @ti_door_positions(tmp2),tmp0
       li   tmp2,3
       bl   @ti_draw_character_and_door
       .endproc
*// ti_draw_door_frame_4

*********************************************************************
*
* Draw a character behind a door
*
* tmp0: screen position in characters
* tmp1: character index to draw
* tmp2: door frame 0-3
*
ti_draw_character_and_door:
       .proc
       .push tmp2                      ; Save door frame
       mov  tmp2,tmp2                  ; Check door frame
       jeq  ti_draw_character_and_door_1 ; If door frame is 0 then don't draw any character
       .push tmp0                      ; Save screen position
       sla tmp2,1                      ; Columns to drawn = door frame * 2
       bl   @ti_draw_partical_character
       .pop tmp0                       ; Restore screen position
ti_draw_character_and_door_1:
       .pop tmp1                       ; Move door frame to tmp1
       bl   @ti_draw_door
       .endproc
*// ti_draw_character_and_door

*********************************************************************
*
* Draw a character
*
* tmp0: screen position in characters
* tmp1: character index to draw
*
ti_draw_character:
       .proc
       sla  tmp1,2                     ; 4 bytes per entry
       ai   tmp1,character_table       ; Add character table base
       mov  *tmp1+,tmp2                ; Get ROM select address
       seto *tmp2                      ; Select ROM bank
       mov  *tmp1,tmp1                 ; Get source address
       sla  tmp0,3                     ; VDP address
       li   tmp2,>060b                 ; Width/height in characters
       bl   @ti_draw_color_graphics    ; Draw graphics
       .endproc
*// ti_draw_character

*********************************************************************
*
* Draw a character pattern without colors
*
* tmp0: screen position in characters
* tmp1: character index to draw
*
ti_draw_character_pattern:
       .proc
       sla  tmp1,2                     ; 4 bytes per entry
       ai   tmp1,character_table       ; Add character table base
       mov  *tmp1+,tmp2                ; Get ROM select address
       seto *tmp2                      ; Select ROM bank
       mov  *tmp1,tmp1                 ; Get source address
       sla  tmp0,3                     ; VDP address
       li   tmp2,>060b                 ; Width/height in characters
       bl   @ti_draw_graphics          ; Draw graphics
       .endproc
*// ti_draw_character_pattern


*********************************************************************
*
* Draw a partial character
*
* tmp0: screen position in characters
* tmp1: character index to draw
* tmp2: number of columns to draw
*
ti_draw_partical_character:
       .proc
       sla  tmp1,2                     ; 4 bytes per entry
       ai   tmp1,character_table       ; Add character table base
       mov  *tmp1+,a                   ; Get ROM select address
       seto *a                         ; Select ROM bank
       mov  *tmp1,tmp1                 ; Get source address
       sla  tmp0,3                     ; VDP address
       li   a,6                        ; Calculate source skip
       s    tmp2,a                     ; 6 - columns to draw
       sla  a,3                        ; x 8
       mov  a,@row_src_skip            ; Set source skip
       sla  tmp2,8                     ; Width to MSB
       ori  tmp2,>000b                 ; Set height
       bl   @ti_draw_color_graphics    ; Draw graphics
       clr  @row_src_skip
       .endproc
*// ti_draw_partial_character

*********************************************************************
*
* Draw a door
*
* tmp0: screen position in characters
* tmp1: frame 0-3
*
ti_draw_door:
       .proc
       seto @doors_bank_select
       sla  tmp1,1                     ; X offset in characters
       a    tmp1,tmp0                  ; Add offset to screen position
       li   tmp2,7                     ; Full door width
       s    tmp1,tmp2                  ; Subtract offset
       mov   @door_frame_table(tmp1),tmp1 ; Source address
       sla  tmp0,3                     ; VDP address
       sla  tmp2,8                     ; Width to MSB
       ori  tmp2,>000b                 ; Set height
       bl   @ti_draw_color_graphics    ; Draw graphics
       .endproc
*// ti_draw_door

*********************************************************************
*
* Scroll screen left
*
ti_scroll_left:
       .proc
       li   tmp0,1
ti_scroll_left_1:
       bl   @ti_dbl_buffer_scroll_screen
       inc  tmp0
       ci   tmp0,12
       jlt  ti_scroll_left_1
       bl   @ti_scroll_cleanup
       .endproc
*// ti_scroll_left

*********************************************************************
*
* Scroll screen right
*
ti_scroll_right:
       .proc
       li   tmp0,31
ti_scroll_right_1:
       bl   @ti_dbl_buffer_scroll_screen
       dec  tmp0
       ci   tmp0,20
       jgt  ti_scroll_right_1
       bl   @ti_scroll_cleanup
       .endproc
*// ti_scroll_right

*********************************************************************
*
* Scroll clean-up
*
ti_scroll_cleanup:
       .proc
       li   r0,name_table
       bl   @vwad
       bl   @ti_init_name_table
       bl   @ti_select_name_table_1
       li   r2,name_table
       bl   @vwad
       bl   @ti_init_name_table
       .endproc
*// ti_scroll_cleanup

*********************************************************************
*
* Scroll screen
*
* tmp0: offset in characters
*
ti_dbl_buffer_scroll_screen:
       .proc
       .push tmp0
       andi tmp0,31
       bl   @ti_scroll_screen
       bl   @ti_vsync
       andi tmp0,1
       jne  scroll_screen_1
       bl   @ti_select_name_table_1
       jmp  scroll_screen_2
scroll_screen_1:
       bl   @ti_select_name_table_2
scroll_screen_2:
       .pop tmp0
       .endproc
*// scroll_screen

*********************************************************************
*
* Scroll screen left by the given offset
*
* tmp0 (r13): offset in characters (0-31)
*
ti_scroll_screen:
       lwpi ti_wrksp
       mov  r13,r0                     ; Offset
       andi r0,1                       ; Odd or even
       sla  r0,10                      ; 0 or >400
       ai   r0,3*32+name_table+>4000   ; VDP address + write bit
       movb @ti_r0lb,@vdpwa            ; Send low byte of VDP RAM write address
       movb r0,@vdpwa                  ; Send high byte of VDP RAM write address
       li   r0,vdpwd
       li   r5,3*32*256                ; Base name for row in MSB
       mov  r13,r6                     ; Offset
       swpb r6                         ; Offset to MSB
       li   r7,>0100                   ; One
       li   r8,>e0ff                   ; Mask (keep 0-31 in MSB)
       li   r4,18                      ; Row counter
ti_scroll_screen_1:
       li   r3,32                      ; Column counter
ti_scroll_screen_2:
       movb r6,r1                      ; Offset
       socb r5,r1                      ; Add base name
       movb r1,*r0                     ; Write to name table
       ab   r7,r6                      ; Increment offset
       szcb r8,r6                      ; Apply mask
       dec  r3
       jne  ti_scroll_screen_2
       movb @vdpsta,@vdp_status
       jlt  ti_scroll_sound
ti_scroll_screen_3:
       ai   r5,>2000                   ; Update base name
       dec  r4
       jne  ti_scroll_screen_1
       lwpi wrksp
       rt

*      Call the module player while scrolling
ti_scroll_sound:
       mov  r0,@save_r0
       mov  r3,@save_r3
       mov  r4,@save_r4
       mov  r5,@save_r5
       mov  r6,@save_r6
       lwpi wrksp
       .push r11
        bl  @module_player_0
       .pop r11
       lwpi ti_wrksp
       mov  @save_r0,r0
       mov  @save_r3,r3
       mov  @save_r4,r4
       mov  @save_r5,r5
       mov  @save_r6,r6
       jmp  ti_scroll_screen_3
save_r0: data 0
save_r3: data 0
save_r4: data 0
save_r5: data 0
save_r6: data 0

*// ti_scroll_screen

*********************************************************************
*
* bc (MSB): number of lines
* bc (LSB): number of bytes
* hl: zx screen address
* de: source address
*
ti_copy_routine:
       .proc
       seto @graphics_bank_select
*      VDP address
       mov  hl,tmp0
       ai   tmp0,-zx_screen
       mov  tmp0,tmp1
       andi tmp0,>00ff                 ; Offset into current 3rd
       sla  tmp0,3                     ; Times 8
       andi tmp1,>1800                 ; 3rd bits same on TI and ZX
       soc  tmp1,tmp0
*      Swap width and height, and convert height to characters
       mov  bc,tmp2
       mov  bc,tmp1
       andi tmp1,>ff00
       srl  tmp1,3
       movb tmp1,tmp2
       swpb tmp2
*      Source
       mov  de,tmp1
       bl   @ti_draw_graphics
       .endproc
*// ti_copy_routine

*********************************************************************
*
* Draw shot for door 1
*
ti_draw_door_1_shot:
       .proc
       li   tmp1,SL0
       bl   @ti_draw_and_hide_door_shot
       .endproc
*// ti_draw_door_1_shot

*********************************************************************
*
* Draw shot for door 2
*
ti_draw_door_2_shot:
       .proc
       li   tmp1,SL0+4
       bl   @ti_draw_and_hide_door_shot
       .endproc
*// ti_draw_door_2_shot

*********************************************************************
*
* Draw shot for door 3
*
ti_draw_door_3_shot:
       .proc
       li   tmp1,SL0+8
       bl   @ti_draw_and_hide_door_shot
       .endproc
*// ti_draw_door_3_shot

*********************************************************************
*
* Draw and hide shot for door
*
ti_draw_and_hide_door_shot:
       .proc
       bl   @ti_draw_door_shot
       bl   @ti_vsync
       bl   @ti_vsync
       bl   @ti_hide_door_shots
       .endproc
*// ti_draw_and_hide_door_shot

*********************************************************************
*
* Draw shot for door
*
ti_draw_door_shot:
       .proc
       li   tmp0,sprite_attr_table
       li   tmp2,4
       bl   @vmbw
       li   tmp1,>d000
       movb tmp1,@vdpwd
       .endproc
*// ti_draw_door_3_shot

*********************************************************************
*
* Hide shots
*
ti_hide_door_shots:
       .proc
       li   tmp0,sprite_attr_table
       li   tmp1,>d000
       bl   @vsbw
       .endproc
*// ti_hide_door_shots

       lwpi ti_wrksp
       li   r0,loading_screen
       li   r1,pattern_table
       li   r2,>1800
       bl   @vmbw
       li   r0,c

*********************************************************************
*
* Show loading creen
*
ti_show_loading_screen:
       .proc
       bl   @ti_display_off
       li   hl,loading_screen
       clr  ix
title_screen_1:
       clr  iy
title_screen_2:
       li   de,pattern_table
       a    ix,de
       a    iy,de
       li   bc,8*32
title_screen_3:
       mov  de,tmp0
       bl   @vwad
       movb *hl+,@vdpwd
       ai   de,8
       dec  bc
       jne  title_screen_3
       inc  iy
       ci   iy,8
       jne  title_screen_2
       ai   ix,8*32*8
       ci   ix,3*8*32*8
       jne  title_screen_1
*      Draw attributes
       li   de,zx_attributes
       li   bc,768
title_screen_4:
       movb *hl+,a
       mov  de,tmp1
       seto @graphics_bank_select
       bl   @ti_draw_attribute
       inc  de
       dec  bc
       jne  title_screen_4
       bl   @ti_display_on
       .endproc
*// ti_show_loading_screen
